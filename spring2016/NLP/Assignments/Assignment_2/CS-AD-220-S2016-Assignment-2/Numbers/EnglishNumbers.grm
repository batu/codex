# Based on Richard Sproat's numbers.grm with modifications
# English Numbers from 0 to 999

zero  = "0" : "zero";

units =   ("1" : "one")
	| ("2" : "two")
	| ("3" : "three")
	| ("4" : "four")
	| ("5" : "five")
	| ("6" : "six")
	| ("7" : "seven")
	| ("8" : "eight")
	| ("9" : "nine")
;

teens =   ("10" : "ten")
	| ("11" : "eleven")
	| ("12" : "twelve")
	| ("13" : "thirteen")
	| ("14" : "fourteen")
	| ("15" : "fifteen")
	| ("16" : "sixteen")
	| ("17" : "seventeen")
	| ("18" : "eighteen")
	| ("19" : "nineteen")
;

decades =   ("2" : "twenty")
	  | ("3" : "thirty")
	  | ("4" : "forty")
	  | ("5" : "fifty")
	  | ("6" : "sixty")
	  | ("7" : "seventy")
	  | ("8" : "eighty")
	  | ("9" : "ninety")
;

ins_space = "" : " ";

ins_hyphen = "" : "-";

ins_and = "" : " and ";

del_zero = "0" : "";

numbers_20_to_99 = decades del_zero | decades ins_hyphen units ;

numbers_to_99 = Optimize[units | teens | numbers_20_to_99] ;

hundreds = units ins_space ("" : "hundred") ;

numbers_100_to_999 = 
Optimize[
  hundreds del_zero del_zero |
  hundreds del_zero ins_and units |
  hundreds ins_and teens |
  hundreds ins_and numbers_20_to_99]
;

numbers_to_999 = Optimize[zero | numbers_to_99 | numbers_100_to_999];

export NUMBERS = Optimize[numbers_to_999];

